Io = <'Io'>;
Meta = <'Meta'>;

expose = func(command) {
    Io.putln("`" + command + "`:");
    Io.put('\t');
    Meta.exec(command);
};

greeter3 = func() {
    self = <'Object'>.new();
    self.default = 'World';
    self.version = 3;

    self.greet = func(input) {
        name = input;
        if !name {
            name = self.default;
        }

        Io.putln("Hello, " + name + "!");
    };

    self.printVersion = func() {
        Io.putln("Greeter version " + self.version);
    };

    ret self;
}();

greeter4 = self -> {
    .default : 'World',
    .version : 4,
    .greet   : func(input) {
        name = input;
        if !name { name = self.default; }
        Io.putln('Hello, ' + name + '!');
    },
    .greetObject : func(obj) {
        Io.putln('Hello, ' + obj.name + '!');
        Io.putln('I see that you are ' + obj.age + ' years old.');
    },
    .printVersion : func() {
        Io.putln("Greeter version " + self.version);
    }
};

simpleObj = {
    .name : "Sam",
    .age  : 20
};

complexObj = {
    .name : "Sam",
    .age  : 20,
    .sayHello : func() {
        Io.putln('Hello, my name is ' + self.name + ' and I am ' + self.age + ' years old.');
    }
} -> self;

evenMoreComplexObj = outer -> {
    .name : "Sam",
    .age  : 20,
    .makeFriendlyOlder : func(byHowMuch) {
        ret inner -> {
            .name : outer.name,
            .age  : outer.age + byHowMuch,
            .sayHello : func() {
                Io.putln('Hello, my name is ' + inner.name + ' and I am ' + inner.age + ' years old.\nI would not have said hi when I was ' + outer.age + '.');
            },
            .render : func() {
                ret {
                    .name : inner.name,
                    .currentAge : inner.age,
                    .previousAge : outer.age,
                    .diffAge : byHowMuch
                };
            }
        };
    }
};

expose('greeter4.greet(simpleObj.name);');
expose('complexObj.sayHello();');
expose('evenMoreComplexObj.makeFriendlyOlder(11).sayHello();');

Io.putln('\nNow for something more interesting (look at code):');
greeter4.greetObject({
    .name : "Foo Bar",
    .age  : 87
});

Meta.repl();

